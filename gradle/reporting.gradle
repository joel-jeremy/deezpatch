apply plugin: "test-report-aggregation"
apply plugin: "jacoco-report-aggregation"

def javaProjects = subprojects.findAll { new File(it.projectDir, "src").exists() }

reporting {
  reports {
    allCodeCoverageReport(JacocoCoverageReport) { 
      testType = "all"
      reportTask.configure {
        def testTasks = javaProjects.stream()
            .flatMap { it.tasks.withType(Test).stream() }
            .toList()

        def execFiles = javaProjects.stream()
            .flatMap { fileTree(it.buildDir).include("/jacoco/*.exec").stream() }
            .toList()

        dependsOn testTasks
        executionData execFiles
      }
    }
    testAggregateTestReport(AggregateTestReport) { 
      testType = TestSuiteType.UNIT_TEST
    }
    integrationTestAggregateTestReport(AggregateTestReport) { 
      testType = TestSuiteType.INTEGRATION_TEST
    }
  }
}

afterEvaluate {
  javaProjects.each {
    it.tasks.named("check").configure {
      dependsOn reporting.reports.collect { it.reportTask }
    }
  }
}

dependencies {
  testReportAggregation javaProjects
  jacocoAggregation javaProjects
}